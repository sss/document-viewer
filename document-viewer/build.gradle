apply plugin: 'com.android.application'

dependencies {
    implementation 'jcifs:jcifs:1.3.17'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.artifex.mupdf:fitz:1.12.0'

    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.android.support:support-annotations:27.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'commons-io:commons-io:2.5'
}

// map for the version code
ext.versionCodes = ['armeabi': 1, 'armeabi-v7a':2, 'x86':3, 'mips':4, 'arm64-v8a':5, 'x86_64':6, 'mips64':7]

import com.android.build.OutputFile

android {
    compileSdkVersion 27
    // copied from: https://github.com/kontalk/androidclient
    // temporarly introduced to fix Cyanogenmod bug
    // https://issuetracker.google.com/issues/64434571#comment35
    buildToolsVersion '27.0.3'

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        applicationId "org.sufficientlysecure.viewer"
        versionCode 2810
        versionName "2.8.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir 'libs'
            jni.srcDirs = [];
        }
        release {
            jni.srcDirs = [];
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "jni/Android.mk"
        }
    }

    splits {
        abi {
            enable true
            reset()
            // include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'mips64', 'x86', 'x86_64'
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk false
        }
    }

    applicationVariants.all { variant ->
      variant.outputs.each { output ->
        output.versionCodeOverride =
          project.ext.versionCodes.get(output.getFilter(OutputFile.ABI)) + defaultConfig.versionCode
      }
    }

    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"
        
        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }
    
    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}

